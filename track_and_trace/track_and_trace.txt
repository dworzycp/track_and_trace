HAI 1.3
    OBTW
        You can find out more about it on their website http://www.lolcode.org/ and https://www.tutorialspoint.com/lolcode/index.htm

        Was this a good choice? Absolutely not! LOLCode has a very limited functionality - so this is very inefficient and I hate myself now
        But it sure was fun :)
       
        TO RUN THIS CODE, GO TO https://www.tutorialspoint.com/execute_lolcode_online.php
        PASTE THIS CODE IN AND HIT EXECUTE

        Summary of the solution*:
        1 - Find all contacts for all people and map it (e.g. P2 -> P1, P3)
        2 - Keep track of people who need to be checked and have who have been checked already
        3 - For people who have not yet been checked, check them
        4 - Go between 2 and 3 until people who need to be checked array is empty

        *ish
    TLDR


    HOW IZ I TRACK_AND_TRACE YR PEOPLE AN YR PATIENT_ZERO
        BTW bukkit is an array, there are no objects in LOLCode, so arrays will have to do
        I HAS A PEOPLE_TO_CONTACT ITZ A BUKKIT
        I HAS A CONTACTS_MAP ITZ A BUKKIT
        I HAS A PEOPLE_TO_CHECK ITZ A BUKKIT
        I HAS A PEOPLE_WHO_HAVE_BEEN_CHECKED ITZ A BUKKIT

        BTW Step 1 - loop through all people to find their contacts
        IM IN YR LOOP UPPIN YR INDEX TIL BOTH SAEM INDEX PEOPLE'Z LENGTH
            I HAS A PERSON ITZ PEOPLE'Z SRS INDEX
            CONTACTS_MAP HAS A SRS INDEX ITZ I IZ FIND_CONTACTS_FOR_ONE_PERSON YR PERSON AN YR PEOPLE MKAY

            BTW Set all people as unchecked
            PEOPLE_TO_CONTACT HAS A SRS INDEX ITZ FAIL
            PEOPLE_WHO_HAVE_BEEN_CHECKED HAS A SRS INDEX ITZ FAIL

            BTW Set patient zero as someone who needs to be checked
            BOTH SAEM PATIENT_ZERO AN PERSON'Z NAME, O RLY?
                YA RLY, PEOPLE_TO_CHECK HAS A SRS INDEX ITZ WIN
                NO WAI, PEOPLE_TO_CHECK HAS A SRS INDEX ITZ FAIL
            OIC
        IM OUTTA YR LOOP

        BTW Step 2 - Figure out contacts with recursive calls
        IM IN YR WRAPPER_LOOP UPPIN YR INDEX_WRAP TIL BOTH SAEM INDEX_WRAP PEOPLE'Z LENGTH
            IM IN YR LOOP UPPIN YR INDEX TIL BOTH SAEM INDEX PEOPLE'Z LENGTH
                I HAS A PERSON ITZ PEOPLE'Z SRS INDEX
                BTW Check if a person is marked for checking and hasn't been checked
                BOTH SAEM WIN AN PEOPLE_TO_CHECK'Z SRS INDEX, O RLY?
                    YA RLY
                        BOTH SAEM FAIL AN PEOPLE_WHO_HAVE_BEEN_CHECKED'Z SRS INDEX, O RLY?
                            YA RLY
                                I HAS A PERSONS_CONTACTS ITZ CONTACTS_MAP'Z SRS INDEX
                                I HAS A CONTACTS_FOR_PERSON ITZ I IZ CONTACT_CONTACTS YR PERSONS_CONTACTS AN YR PEOPLE_TO_CONTACT AN YR PEOPLE MKAY
                                PEOPLE_TO_CONTACT R CONTACTS_FOR_PERSON
                                PEOPLE_TO_CHECK R CONTACTS_FOR_PERSON
                                BTW No longer need to check this person
                                PEOPLE_TO_CHECK'Z SRS INDEX R FAIL
                        OIC
                OIC
            IM OUTTA YR LOOP
        IM OUTTA YR WRAPPER_LOOP

        BTW Concat people who need to be contacted and return them
        I HAS A RTN_VAL ITZ ""
        IM IN YR LOOP UPPIN YR INDEX TIL BOTH SAEM INDEX PEOPLE'Z LENGTH
            I HAS A PERSON ITZ PEOPLE'Z SRS INDEX
            BTW Exclude patient zero
            DIFFRINT PERSON'Z NAME AN PATIENT_ZERO, O RLY?
                YA RLY
                    BOTH SAEM WIN AN PEOPLE_TO_CONTACT'Z SRS INDEX, O RLY?
                        YA RLY, RTN_VAL R SMOOSH RTN_VAL PERSON'Z NAME " " MKAY
                    OIC
            OIC
        IM OUTTA YR LOOP
        FOUND YR RTN_VAL
    IF U SAY SO

    HOW IZ I CONTACT_CONTACTS YR PERSONS_CONTACTS AN YR PEOPLE_TO_CONTACT AN YR PEOPLE
        BTW Check if person is in infected person's contacts
        IM IN YR LOOP UPPIN YR INDEX TIL BOTH SAEM INDEX PEOPLE'Z LENGTH
            BOTH SAEM WIN AN PERSONS_CONTACTS'Z SRS INDEX, O RLY?
                YA RLY
                    PEOPLE_TO_CONTACT'Z SRS INDEX R WIN
            OIC
        IM OUTTA YR LOOP

        FOUND YR PEOPLE_TO_CONTACT
    IF U SAY SO

    HOW IZ I FIND_CONTACTS_FOR_ONE_PERSON YR PERSON_TO_CHECK AN YR PEOPLE
        I HAS A CONTACTS ITZ A BUKKIT

        IM IN YR LOOP UPPIN YR INDEX TIL BOTH SAEM INDEX PEOPLE'Z LENGTH
            CONTACTS HAS A SRS INDEX ITZ FAIL
            I HAS A PERSON ITZ PEOPLE'Z SRS INDEX
            BTW Loop through people's locations, expect for PERSON_TO_CHECK
            DIFFRINT PERSON'Z NAME AN PERSON_TO_CHECK'Z NAME, O RLY?
                YA RLY
                    BTW Check if a person has visited an infected location
                    IM IN YR INFECTED_LOOP UPPIN YR INDEX_INF TIL BOTH SAEM INDEX_INF PERSON_TO_CHECK'Z NUM_OF_LOCATIONS
                        IM IN YR LOCATION_LOOP UPPIN YR INDEX_LOC TIL BOTH SAEM INDEX_LOC PERSON'Z NUM_OF_LOCATIONS
                            BOTH SAEM PERSON'Z LOCATIONS'Z SRS INDEX_LOC AN PERSON_TO_CHECK'Z LOCATIONS'Z SRS INDEX_INF, O RLY?
                                YA RLY, CONTACTS'Z SRS INDEX R WIN
                            OIC
                        IM OUTTA YR LOCATION_LOOP
                    IM OUTTA YR INFECTED_LOOP
            OIC
        IM OUTTA YR LOOP

        FOUND YR CONTACTS
    IF U SAY SO

    OBTW
        Sadly, LOLCode does not have a testing framework
        Below, I set up the data which was provided in the Codewars example
        Call the main function and check that the response from it is P1 and P2
    TLDR

    BTW Set up person 1
    I HAS A P1 ITZ A BUKKIT
    P1 HAS A NAME ITZ "P1"
    P1 HAS A NUM_OF_LOCATIONS ITZ 3
    I HAS A P1_LOCATIONS ITZ A BUKKIT
    P1_LOCATIONS HAS A SRS 0 ITZ 1
    P1_LOCATIONS HAS A SRS 1 ITZ 2
    P1_LOCATIONS HAS A SRS 2 ITZ 8
    P1 HAS A LOCATIONS ITZ P1_LOCATIONS

    BTW Set up person 2
    I HAS A P2 ITZ A BUKKIT
    P2 HAS A NAME ITZ "P2"
    P2 HAS A NUM_OF_LOCATIONS ITZ 3
    I HAS A P2_LOCATIONS ITZ A BUKKIT
    P2_LOCATIONS HAS A SRS 0 ITZ 1
    P2_LOCATIONS HAS A SRS 1 ITZ 6
    P2_LOCATIONS HAS A SRS 2 ITZ 7
    P2 HAS A LOCATIONS ITZ P2_LOCATIONS

    BTW Set up person 3
    I HAS A P3 ITZ A BUKKIT
    P3 HAS A NAME ITZ "P3"
    P3 HAS A NUM_OF_LOCATIONS ITZ 3
    I HAS A P3_LOCATIONS ITZ A BUKKIT
    P3_LOCATIONS HAS A SRS 0 ITZ 4
    P3_LOCATIONS HAS A SRS 1 ITZ 7
    P3_LOCATIONS HAS A SRS 2 ITZ 9
    P3 HAS A LOCATIONS ITZ P3_LOCATIONS

    BTW Set up person 4
    I HAS A P4 ITZ A BUKKIT
    P4 HAS A NAME ITZ "P4"
    P4 HAS A NUM_OF_LOCATIONS ITZ 2
    I HAS A P4_LOCATIONS ITZ A BUKKIT
    P4_LOCATIONS HAS A SRS 0 ITZ 3
    P4_LOCATIONS HAS A SRS 1 ITZ 5
    P4 HAS A LOCATIONS ITZ P4_LOCATIONS

    I HAS A PEOPLE ITZ A BUKKIT
    PEOPLE HAS A LENGTH ITZ 4
    PEOPLE HAS A SRS 0 ITZ P1
    PEOPLE HAS A SRS 1 ITZ P2
    PEOPLE HAS A SRS 2 ITZ P3
    PEOPLE HAS A SRS 3 ITZ P4

    I HAS A PATIENT_ZERO ITZ A YARN
    PATIENT_ZERO R "P3"

    I HAS A PEOPLE_TO_CONTACT ITZ I IZ TRACK_AND_TRACE YR PEOPLE AN YR PATIENT_ZERO MKAY
    VISIBLE SMOOSH "People who need to be contacted - " PEOPLE_TO_CONTACT MKAY

    I HAS A EXPECTED_RESULTS ITZ "P1 P2 "
    BOTH SAEM PEOPLE_TO_CONTACT AN EXPECTED_RESULTS, O RLY?
        YA RLY, VISIBLE "Test passed"
        NO WAI, VISIBLE SMOOSH "Test failed, received (" PEOPLE_TO_CONTACT ") EXPECTED (" EXPECTED_RESULTS ")" MKAY
    OIC

    VISIBLE "Remeber: wash your hands, cover your face and make space!"
KTHXBYE
